
model{
#1A. Diffuse normal priors beta
for (i in 1:2) { beta[i] ~ dnorm(0, 0.0001)}
#1B. Diffuse uniform prior sigma
sigma ~ dunif(0, 20)
tau  <- 1 / (sigma * sigma)
#1C. Priors random intercepts and random slopes
for (i in 1:17) { a[i] ~ dnorm(0, tau1_Site) }
for (i in 1:17) { b[i] ~ dnorm(0, tau2_Site) }
#1D. Diffuse uniform prior for sigma1_Site
#    This one is for the random intercept
tau1_Site  <- 1 / (sigma1_Site * sigma1_Site)
sigma1_Site ~ dunif(0, 100)
#1E. Diffuse uniform prior for sigma2_Site
#    This one is for the random slope
tau2_Site  <- 1 / (sigma2_Site * sigma2_Site)
sigma2_Site ~ dunif(0, 100)
########################
#2. Likelihood (with lillies specific coding)
for (i in 1:356) {
MD[i]   ~ dnorm(mu[i], tau)
eta[i] <- beta[1] + beta[2] * PD.std[i] #Covariates
mu[i]  <- eta[i]  + a[Site[i]] + b[Site[i]] * PD.std[i]
#Covariates + random intercepts + random slopes
}
#Residuals
for (i in 1:356) {
Res[i]    <- MD[i] - mu[i]
}
}

