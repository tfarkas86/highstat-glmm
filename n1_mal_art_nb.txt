
    model{
    #1A. Priors beta and sigma
    for (i in 1:K) { beta[i] ~ dnorm(0, 0.0001)}
    
    #1C. Prior for size
    num.s   ~ dnorm(0, 0.0016)            #<----from squirrel exercise
    denom.s ~ dnorm(0, 1)                 #<----from squirrel exercise
    size <- abs(num.s / denom.s)          #<----from squirrel exercise
    
    
    #2. Likelihood
    for (i in 1:N) {
    Y[i] ~  dnegbin(p[i], size)
    p[i] <- size / (size + mu[i])  
    
    log(mu[i]) <- eta[i] 
    eta[i]     <- inprod(beta[], X[i,]) 
    
    #3. New stuff: Discrepancy measures 
    Exp[i] <- mu[i] 
    Var[i] <- mu[i] + mu[i] * mu[i] / size
    
    #Pearson residuals
    E[i]   <- (Y[i]  - Exp[i]) / sqrt(Var[i])    
    
    #Simulated NB data 
    YNew[i] ~  dnegbin(p[i], size) 
    
    #Pearson residuals for simulated data
    ENew[i] <- (YNew[i] - Exp[i]) / sqrt(Var[i]) 
    D[i]    <- pow(E[i], 2)                      
    DNew[i] <- pow(ENew[i], 2)   
    }          
    Fit         <- sum(D[1:N])      #Sum of squared residuals  
    FitNew      <- sum(DNew[1:N])  #Sum of squared predicted residuals
    }
    
